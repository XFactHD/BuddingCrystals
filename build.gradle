plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.77'
}

version = mod_version
group = 'xfacthd.buddingcrystals'

base {
    archivesName = 'BuddingCrystals'
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

runs {
    configureEach {
        // Recommended logging data for a userdev environment
        //systemProperty 'forge.logging.markers', 'REGISTRIES'
        // Recommended logging level for the console
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        workingDirectory project.file('run')
    }

    server {
        workingDirectory project.file('run_server')
        programArgument '--nogui'
    }

    data {
        workingDirectory project.file('run')

        // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
        programArguments.addAll(
                '--mod', 'buddingcrystals',
                '--all',
                '--output', file('src/generated/resources/').getAbsolutePath(),
                '--existing', file('src/main/resources/').getAbsolutePath(),
                '--existing-mod', 'ae2',
                '--existing-mod', 'mekanism'
        )
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
}

dependencies {
    implementation("net.neoforged:neoforge:${neo_version}")

    //runtimeOnly("curse.maven:jei-238222:4595492")

    // Dynamic Asset Generator
    implementation("curse.maven:dag-577096:4900589")

    // Compat mods
    //runtimeOnly("curse.maven:ae2-223794:4907420")
    //runtimeOnly("curse.maven:mekanism-268560:4385637")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version: neo_version,
            neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_version: mod_version
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
